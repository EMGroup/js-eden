/**
 * @title Worksheet Template
 * @author Nicolas Pope
 */

require("Page");

import worksheet/style;

if (worksheet_title == @) worksheet_title = "Worksheet 1";
if (worksheet_subtitle == @) worksheet_subtitle = "Subtitle goes here";
if (worksheet_content == @) worksheet_content = "Some content goes here";

ws_title_text is Element with
	style is WorksheetTitleTextStyle,
	content is worksheet_title;

ws_subtitle_text is Element with
	style is WorksheetSubTitleTextStyle,
	content is worksheet_subtitle;

ws_title is Element with
	style is WorksheetTitleStyle,
	content is [ws_title_text, ws_subtitle_text];

ws_content is Element with
	style is WorksheetContentStyle,
	content is worksheet_content;

WorksheetPage is [ws_title, ws_content];

if (worksheet_pages == @) worksheet_pages is [WorksheetPage];
worksheet_currentpage is worksheet_pages[worksheet_page];

if (document == @) document is worksheet_currentpage;

if (worksheet_page == @) worksheet_page = 1;

action nextPage {
	worksheet_page++;
}

action prevPage {
	worksheet_page--;
}

${{
edenpagechangelock = false;
window.onpopstate = function(e) {
	if (e.state && e.state.wspage !== undefined) {
		edenpagechangelock = true;
		eden.root.lookup("worksheet_page").assign(e.state.wspage, eden.root.scope);
	}
}
}}$;

when (worksheet_page) {
	${{
	if (!edenpagechangelock) {
		history.pushState({wspage: eden.root.lookup("worksheet_page").value()}, "Worksheet");
	}
	edenpagechangelock = false;
	}}$;
}

