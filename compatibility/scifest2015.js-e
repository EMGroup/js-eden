##Shim construal scifest2015
##
##Run on the later versions of JS-EDEN to temporarily reverse incompatible
##changes made to the environment subsequent to the SciFest 2015 event at Joensuu (21-27 April 2015)
##but before the next major JS-EDEN release.

_view_list is _views_list;

##Form controls used to take a "name" parameter.

func Button {
${{
	var name = arguments[0];
	var label = arguments[1];
	var x = arguments[2];
	var y = arguments[3];
	var enabled = arguments[4];
	var fillcolour = arguments[5];
	var textcolour = arguments[6];
	if (enabled === undefined) enabled = true;
	return new Button(name, label, x, y, enabled, fillcolour, textcolour);
}}$;
}

func Checkbox { ${{
  var name = arguments[0];
  var x = arguments[1];
  var y = arguments[2];
  var tristate = arguments[3];
  if (tristate === undefined) tristate = false;
  var enabled = arguments[4];
  if (enabled === undefined) enabled = true;
  return new Checkbox(name, x, y, tristate, enabled);
}}$; }

func Combobox { ${{
  var name = arguments[0];
  var values = arguments[1];
  var x = arguments[2];
  var y = arguments[3];
  var enabled = arguments[4];
  return new DropDownList(name, values, values, x, y, enabled);
}}$; }

func RadioButtons { ${{
	var name = arguments[0];
	var values = arguments[1];
	var x = arguments[2];
	var y = arguments[3];
	var enabled = arguments[4];
	var horizontal = arguments[5];
	return new RadioButtons(name, values, values, x, y, undefined, horizontal, enabled);
}}$; }

func Slider { ${{
  var name = arguments[0];
  var min = arguments[1];
  var max = arguments[2];
  var step = arguments[3];
  var x = arguments[4];
  var y = arguments[5];
  var length = arguments[6];
  var orientation = arguments[7];
  if (orientation === undefined) orientation = "horizontal";
  var enabled = arguments[8];
  if (enabled === undefined) enabled = true;
  return new Slider(name, min, max, step, x, y, length, orientation, enabled);
}}$; }

func Textbox { ${{
	var name = arguments[0];
	var x = arguments[1];
	var y = arguments[2];
	var width = arguments[3];
	var height, placeholder, enabled;
	var argsProcessed = 4;
	var arg = arguments[argsProcessed];
	var argType = typeof(arg);
	if (arg === undefined || argType == "number") {
		height = arg;
		argsProcessed++;
		arg = arguments[argsProcessed];
		argType = typeof(arg);
	}
	if (arg === undefined || argType == "string") {
		placeholder = arg;
		argsProcessed++;
		arg = arguments[argsProcessed];
	}
	enabled = arg;
	return new Textbox(name, x, y, width, height, placeholder, undefined, enabled);
}}$; }

func indexOf {
	para searchValue, target, start, isSorted;
	if (isString(target)) {
		return substringPosition(searchValue, target, start);
	} else {
		return positionInList(searchValue, target, start, isSorted);
	}
}

indexOfRE is positionOfRE;

##Default to base e instead of base 10.
func log {
	${{
		var number = arguments[0];
		var base = arguments[1];
		if (arguments.length == 0 || arguments.length > 2) {
			eden.error(new Error("log: The function requires at least one argument and at most two arguments."), "error");
			return undefined;
		}

		if (number === undefined || (arguments.length == 2 && base === undefined)) {
			return undefined;
		} else if (typeof(number) != "number") {
			eden.error(new Error("log: The first argument must be a number, not a " + typeof(number)), "error");
		} else if (base !== undefined && typeof(base) != "number") {
			eden.error(new Error("log: The second argument must be a number, not a " + typeof(base)), "error");
		}

		if (base === undefined) {
			return Math.log(number);
		} else {
			return Math.log(number) / Math.log(base);
		}
	}}$;
}

##Linear algebra stuff

func centroid {
  para points;
  auto i, x, y;

  x = 0;
  y = 0;
  i = 0;

  for(i = 1; i < points#; i = i + 2) {
    x = x + points[i];
    y = y + points[i+1];
  }

  x = x / (points# / 2);
  y = y / (points# / 2);
  
  return [x,y];
}

func rotate {
  para points, origin, degrees;
  auto i, c, s, x, y, rpoints;

  ##Translate to origin.
  rpoints = translate(points, [-origin[1],-origin[2]]);

  c = cos(degrees);
  s = sin(degrees);

  /* Rotate clockwise about origin */
  for(i = 1; i < points#; i = i + 2) {
    x = rpoints[i];
    y = rpoints[i+1];
    rpoints[i] = x*c - y*s;
    rpoints[i+1] = x*s + y*c;
  }

  /* Tranlate to original location */
  rpoints = translate(rpoints, [origin[1],origin[2]]);

  return rpoints;
}

func scale {
	para scaleFactor, vector;
	auto result, i;
	if ((scaleFactor == @ || vector == @) && $# == 2) {
		return @;
	}
	if (!isNumber(scaleFactor)) {
		error("scale: The first argument must be a number, not a " // type(scaleFactor));
		return @;
	} else if (!isList(vector)) {
		error("scale: The second argument must be a list, not a " // type(vector));
		return @;
	}
	result = array(vector#);
	for (i = 1; i <= vector#; i++) {
		result[i] = scaleFactor * vector[i];
	}
	return result;
}

func translate {
  para points, vector;
  auto i, tpoints;

  i = 0;
  tpoints = [];

  for(i = 1; i < points#; i = i + 2) {
    append tpoints, points[i] + vector[1];
    append tpoints, points[i+1] + vector[2];
  }

  return tpoints;
}

${{
	Eden.systemObservableNames["_view_list"] = true;
	Eden.systemFunctionNames["indexOf"] = true;
	Eden.systemFunctionNames["indexOfRE"] = true;
	Eden.systemFunctionNames["rotate"] = true;
	Eden.systemFunctionNames["scale"] = true;
	Eden.systemFunctionNames["translate"] = true;
}}$;