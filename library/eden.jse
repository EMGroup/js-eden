## Copyright (c) 2013, Empirical Modelling Group
## All rights reserved.
##
## See LICENSE.txt

##MISSING FUNCTIONS

proc eager {
	if (autocalc == 0) {
		autocalc = 1;
		autocalc = 0;
	};
};

func time {
  ${{
  var now = new Date();
  return Math.floor(now.getTime() / 1000);
  }}$;
};

proc writeln {
  ${{
  var t = arguments[0];
  console.log(t);
	if (edenUI.plugins.MenuBar) {
		edenUI.plugins.MenuBar.updateStatus("Output: "+t);
	}
  }}$;
};

func random { return ${{ Math.random(); }}$; };

func srand {

};

##Raises an error if fewer or greater than two arguments are given.
##Returns undefined if either argument is undefined.
##tkeden returns strange strings beginning with the word "usage" in these situations.
func apply {
	${{
		if (arguments.length != 2) {
			eden.error(new Error("apply: This function requires exactly 2 arguments."), "error");
			return undefined;			
		} else if (typeof(arguments[0]) != "function") {
			eden.error(new Error("apply: The first argument must be of type func or type proc, not " + typeof(arguments[0])), "error");
			return undefined;
		} else if (!Array.isArray(arguments[1])) {
			eden.error(new Error("apply: The second argument must be of type list, not " + typeof(arguments[1])), "error");
			return undefined;
		} else {
			return arguments[0].apply(this, arguments[1]);
		}
	}}$;
}

func forget {
	${{
		if (arguments.length != 1) {
			eden.error(new Error("forget: This function requires exactly 1 argument."), "error");
			if (arguments.length == 0) {
				return 1;
			}
		}
	
		var nameToDelete, symbolToDelete;
		
		if (typeof(arguments[0]) == "string") {
			nameToDelete = arguments[0];
			symbolToDelete = root.lookup(nameToDelete);
			if (!(nameToDelete in root.symbols)) {
				return 1;
			}
			nameToDelete = "/" + nameToDelete;
		} else if (arguments[0] instanceof Symbol) {
			symbolToDelete = arguments[0];
			nameToDelete = symbolToDelete.name;
		} else {
			eden.error(new Error("forget: The argument must be a string or a pointer, not a " + typeof(arguments[0])), "error");
			return 1;
		}

		for (var dependency in symbolToDelete.subscribers) {
			return 2;
		}
		for (var triggeredProc in symbolToDelete.observers) {
			return 2;
		}

		for (var triggerName in symbolToDelete.observees) {
			var triggerSymbol = root.lookup(triggerName.slice(1));
			delete triggerSymbol.observers[nameToDelete];
		}
		delete root.symbols[nameToDelete.slice(1)];
		return 0;
	}}$;
};

proc error {
	${{
		if (arguments.length > 1) {
			eden.error(new Error("error: This procedure requires at most 1 argument"), "error");
		}
		if (arguments.length == 0) {
			eden.error(new Error("Runtime error"), "error");
		} else {
			eden.error(new Error(arguments[0]), "error");
		}
	}}$;
}

func nameof {
	${{
		if (arguments.length != 1) {
			eden.error(new Error("nameof: This function requires exactly 1 argument."), "error");
			return undefined;
		}

		var pointer = arguments[0];
		
		if (pointer instanceof Symbol) {	
			return pointer.name.slice(1);
		} else if (
			pointer !== null &&
			typeof(pointer) == "object" &&
			"keys" in pointer &&
			Array.isArray(pointer.keys) &&
			pointer.keys.length > 0 &&
			typeof(pointer.keys[0]) == "number" &&
			"parent" in pointer &&
			pointer.parent instanceof Symbol
		) {
			return pointer.parent.name.slice(1) + "[" + pointer.keys[0] + "]";
		} else {
			eden.error(new Error("nameof: The argument must be a pointer, not a " + typeof(pointer)), "error");
			return undefined;
		}
	}}$;
}

proc todo {
	auto code;
	if ($# != 1) {
		error("todo: This proc requires exactly 1 argument.");
		return;
	}
	
	code = $[1];
	
	if (code == @) {
		return;
	} else if (!isString(code)) {
		error("todo: The argument must be of type string, not " // type(code));
		return;
	}
	
	code = code // ";";
	
	after (0) {
		execute(code);
	}
}

##A new JS-EDEN specific function.
func doDefault {
	para value, defaultValue;
	if ($# != 2) {
		error("doDefault: This function requires exactly 2 arguments.");
	}
	if (value == @) {
		if (defaultValue == @) {
			error("doDefault: The default value cannot be undefined.");
		} else {
			return defaultValue;
		}
	} else {
		return value;
	}
}

##Autocalc compatibility
autocalc = 1;

##Include the rest of the library

require("MenuBar");

include("library/types.js-e");
include("library/core.js-e");
include("library/lists.js-e");
include("library/maths.jse");
include("library/strings.js-e");
include("library/algebra.jse");
include("library/include_js.js-e");
include("library/dynamic.js-e");
include("library/html.js-e");
include("library/objects.js-e");
include("library/declarevar.js-e");

require("InputWindow");
require("PluginListing");
require("ProjectList");
require("CanvasHTML5");
require("SymbolViewer");
require("HTMLViews");
require("SLT");
require("SG");
require("DM");
require("ST");
createCanvas("picture");
createView("projects", "ProjectList");
createView("inputwindow", "InputWindow");

_menubar_status = "JS-Eden done loading";

proc arrange {
  auto i;
  auto name;
  auto curr_x;

  curr_x = 0;
  for (i = 1; i <= _view_list#; ++i) {
    name = _view_list[i];
    if (`"_view_"//name//"_position"` != @) {
      continue;
    }
    `"_view_"//name//"_x"` = curr_x;
    `"_view_"//name//"_y"` = 100;
    curr_x += `"_view_"//name//"_width"` + 10;
  }
}

arrange();
