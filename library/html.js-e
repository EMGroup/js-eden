## Copyright (c) 2014, Empirical Modelling Group
## All rights reserved.
##
## See LICENSE.txt
##
##Functions for mapping between content or styling described by EDEN observables and syntactically
##correct HTML mark-up or CSS.

##Given an EDEN list of strings, returns a string containing HTML mark-up for formatting the given
##items as an unordered (bullet point) list.
func bulletList {
	auto items, result, i;
	if ($# == 1 && isList($[1])) {
		items = $[1];
	} else {
		items = $;
	}
	
	if (items# == 0) {
		return "";
	}
	
	result = "<ul>";
	
	for (i = 1; i <= items#; i++) {
		if (isString(items[i])) {
			result = result // "<li style=\"margin-bottom: 7pt; line-height: 150%\">" // items[i] // "</li>\n";
		} else if (items[i] != @) {
			error("bulletList: Item " // str(i) // " must be a string, not a " // type(items[i]));
			return @;
		}
	}
	
	result = result // "</ul>\n";
	return result;
}
