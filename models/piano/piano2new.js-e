##In this version the detection of which key has been clicked is optimized so
##that the nearest white key is calculated first (in a more declarative manner
##than before) and then the boundary is checked to see if the user actually
##clicked in one or other of the black keys on either side of the white key.
##Despite preferring formulaic calculation over procedural iteration the
##algorithm is more difficult to understand, in my opinion.

##Items of interest: drawOrderToPianoKeyNum (renamed from keyboardToKey), pianoKeyNumToDrawOrder, playNoteWhenClicked, backupHighlightedKeys

##Outstanding bugs:
##Notes from F#7 onwards don't sound as high as they should.  The default sampling
##rate is 11025, which becomes less than 4 samples per cycle for these high notes,
##which is probably insufficient.  Need to delve into the audio library more deeply
##and learn how to override the default.

include("compatibility/construit.c5.js-e");

##Include the JavaScript sound library.
include("http://www.dcs.warwick.ac.uk/~wmb/PianoJS/audio.js");

##=============================================================================
##Built-in EDEN functions not yet built into JS-EDEN.

## TODO: These are in draft form and might produce results inconsistent with
## EDEN for edge cases.

proc error {
	${{
		eden.error(new Error(arguments[0]));
	}}$;
}

func substr {
	${{
		return arguments[0].slice(arguments[1] - 1, arguments[2]);
	}}$;
}

##int("A") incorrectly gives NaN instead of 65, so this is a replacement function.
func charCode {
	${{
		if (arguments[0].length == 1) {
			return arguments[0].charCodeAt();
		} else {
			return undefined;
		}
	}}$;
}

func char {
	${{
		if (typeof(arguments[0]) == "number") {
			return String.fromCharCode(arguments[0]);
		} else if (typeof(arguments[0]) == "string") {
			return arguments[0].charAt(0);
		} else {
			return undefined;
		}
	}}$;
}

##=============================================================================
##Useful functions for lists.

##Returns true if the second argument is found somewhere in the list given by
##the first argument.
func inList {
	para list, item;
	auto i;
	for (i = 1; i <= list#; i++) {
		if (list[i] == item) {
			return true;
		}
	}
	return false;
}

##=============================================================================
##Useful functions for strings.

##Returns the string formed by replacing the first instance of the second
##argument with the string given by the third argument in the strin the string
##given by the first argument (or all instances if the second argument is a
##regular expression of the form /.../g instead of a plain string.
func replace {
	${{
		return arguments[0].replace(arguments[1], arguments[2]);
	}}$;
}

##=============================================================================
##Functions and procedures for assisting with the creation and comprehension of
##definitions created dynamically at runtime from strings.

##Create an aditional JS-EDEN view that logs each definition, function or procedure
##just after it has been successfully declared using the "execute" procedure.
##The resulting window displays a script that can be used as a tool for
##comprending the procedural code that created the definitions.  In simple
##cases of using "execute" to create many similar definitions the contents of
##this window could be directly substituted for the procedural code.
createView("Dynamically_Created_Definitions", "PlainHTML");
generatedDefinitions = "";

##A drop-in replacement for "execute" that "execute"s its argument and logs the
##(re-)definition to the log window.
proc createDefinition {
	para definition;
	auto definitionHTML;
	execute(definition);
	definitionHTML = definition;
	definitionHTML = replace(definitionHTML, "<", "&lt;");
	definitionHTML = replace(definitionHTML, ">", "&gt;");
	generatedDefinitions = generatedDefinitions // "<div>" // definitionHTML // "</div>\n";
	html("Dynamically_Created_Definitions", generatedDefinitions);
}

##This function is the reverse of execute/createDefinition.  Given a string
##that names an observable it returns the code used to define that
##observable.
func definitionOf {
	${{
		function edenCodeForValue(value) {
			var code = "";
			if (typeof(value) == "undefined") {
				code = "@";
			} else if (typeof(value) == "string") {
				code = "\"" + value + "\"";
			} else if (value == null) {
				code = "@";
			} else if (Array.isArray(value)) {
				code = "[";
				for (var i = 0; i < value.length - 1; i++) {
					code = code + edenCodeForValue(value[i]) + ", ";
				}
				if (value.length > 0) {
					code = code + edenCodeForValue(value[value.length - 1]);
				}
				code = code + "]";
			} else {
				code = value.toString();
			}
			return code;
		}

		var name = arguments[0];
		var symbol = root.lookup(name);
		if (symbol.eden_definition != null) {
			return symbol.eden_definition + ";";
		} else {
			return name + " = " + edenCodeForValue(symbol.cached_value) + ";";
		}
	}}$;
}

##Executes a string observable containing JavaScript code.
proc execute_js {
	${{
		eval(arguments[0].replace(/\n/g, ""));
	}}$;
}

##=============================================================================
##Piano
##All names of notes use the English naming convention A..G unless stated
##otherwise.  Where it is use useful to distinguish between two notes an
##octave apart the scientific naming convention is used, giving names for
##the keys on a standard piano keyboard in the sequence A0..C8.

##Map the JavaScript sound library onto some observables.
${{
	declare_jse("toneGenerator");
	var toneGenerator = DataGenerator;
}}$;

##Calculate the number of diatonic steps a note is away from the C that is
##either itself or else is the nearest C immediately below itself, with results
##in the range 0-7.  One diatonic step corresponds to a one letter increment
##through the English alphabet.  The argument should not include the octave
##number.
func diatonicStepsFromC {
	para letter;
	auto asciiNumber;
	
	## C D E F G A B
	## 67686970716566 charCode()
	## 7 8 9 10115 6  -60
	## 0 1 2 3 4 5 6  %7
	
	##N.B. Unlike piano key numbers this sequence doesn't have positions for
	##the sharps, which follows on directly from the fact that the position
	##of a sharp on the stave is the same as it's nearest nearest natural
	##note and then we have key signatures and accidentals notation to
	##determine the exact note.
	
	asciiNumber = charCode(letter);
	if (asciiNumber < 65 || asciiNumber > 71) {
		error("Unknown note: " // letter);
		return @;
	} else {
		return (asciiNumber - 60) % 7;
	}
}

##Calculates the letter of the note that is a given non-negative number of
##diatonic steps from C.
func diatonicStepsFromCToLetter {
	para number;
	
	## 0 1 2 3 4 5 6
	## 2 3 4 5 6 7 8  +2
	## 2 3 4 5 6 0 1  %7
	## 67686970716566 +65
	## C D E F G A B  char()
	
	return char((number + 2) % 7 + 65);
}

naturalSymbol = "n";
sharpSymbol = "#";
flatSymbol = "b";
doubleSharpSymbol = "x";
doubleFlatSymbol = "bb";

##Convert a music note (by scientific name) to its piano key number.  E.g. A4 -> 49
func pianoKeyNumber {
	para noteName;
	auto noteNameWithoutOctave, octave, keyNumber;
	if (substr(noteName, 2, 1 + sharpSymbol#) == sharpSymbol) {
		noteNameWithoutOctave = substr(noteName, 1, 2);
		octave = int(substr(noteName, 3, noteName#));
	} else if (substr(noteName, 2, 1 + flatSymbol#) == flatSymbol) {
		noteNameWithoutOctave = diatonicStepsFromCToLetter(diatonicStepsFromC(noteName[1]) + 6) // sharpSymbol;
		octave = int(substr(noteName, 3, noteName#));
		if (noteNameWithoutOctave[1] > noteName[1]) {
			octave--;
		}
	} else {
		noteNameWithoutOctave = noteName[1];
		octave = int(substr(noteName, 2, noteName#));	
	}
	if (noteNameWithoutOctave == "A") {
		keyNumber = 1;
	} else if (noteNameWithoutOctave == "A" // sharpSymbol) {
		keyNumber = 2;
	} else if (noteNameWithoutOctave == "B") {
		keyNumber = 3;
	} else if (noteNameWithoutOctave == "C") {
		keyNumber = 4;
	} else if (noteNameWithoutOctave == "C" // sharpSymbol) {
		keyNumber = 5;
	} else if (noteNameWithoutOctave == "D") {
		keyNumber = 6;
	} else if (noteNameWithoutOctave == "D" // sharpSymbol) {
		keyNumber = 7;
	} else if (noteNameWithoutOctave == "E") {
		keyNumber = 8;
	} else if (noteNameWithoutOctave == "F") {
		keyNumber = 9;
	} else if (noteNameWithoutOctave == "F" // sharpSymbol) {
		keyNumber = 10;
	} else if (noteNameWithoutOctave == "G") {
		keyNumber = 11;
	} else if (noteNameWithoutOctave == "G" // sharpSymbol) {
		keyNumber = 12;
	} else {
		error("Unknown note " // noteName);
		keyNumber = @;
	}
	
	if (keyNumber < 4) {
		keyNumber = keyNumber + octave * 12;
	} else {
		keyNumber = keyNumber + (octave - 1) * 12;
	}
	return keyNumber;
}

##Convert a piano key number into the scientific name of the note.  E.g. 50 -> A#4
func noteName {
	para keyNumber;
	auto octave, keyNumberInFirstOctave;
	keyNumberInFirstOctave = keyNumber % 12;
	octave = int(keyNumber / 12) + 1;
	if (keyNumberInFirstOctave < 4) {
		octave--;
	}
	
	switch (keyNumberInFirstOctave) {
	case 0:
		return "G" // sharpSymbol // octave;
	case 1:
		return "A" // octave;
	case 2:
		return "A" // sharpSymbol // octave;
	case 3:
		return "B" // octave;
	case 4:
		return "C" // octave;
	case 5:
		return "C" // sharpSymbol // octave;
	case 6:
		return "D" // octave;
	case 7:
		return "D" // sharpSymbol // octave;
	case 8:
		return "E" // octave;
	case 9:
		return "F" // octave;
	case 10:
		return "F" // sharpSymbol // octave;
	case 11:
		return "G" // octave;
	}
}

##Piano key dimensions are taken from:
## http://www.quadibloc.com/other/cnv05.htm
pianoPosition = Point(0, 0);
naturalKeyWidth = 20;
naturalKeyLength is (5 + 3/8) / (0.864 * 1.066) * naturalKeyWidth;
sharpKeyWidth is 20 / 35 * naturalKeyWidth;
sharpKeyLength is 3.5 / 5.5 * naturalKeyLength; ##Measured from Meurig's keyboard.

##Returns a Drawable that represents the piano key corresponding to a given piano key number.
func keyVisual {
	para keyNumber, xOffset, yOffset, whiteKeyWidth, whiteKeyLen, blackKeyWidth, blackKeyLen, highlightColour;
	auto xPos, keyWidth, keyHeight, colour;
	xPos = xOffset + int((keyNumber - 1) / 12) * 7 * whiteKeyWidth;
	switch ((keyNumber - 1) % 12) {
	case 0: ## A
		keyWidth = whiteKeyWidth;
		keyHeight = whiteKeyLen;
		colour = "white";
		break;
	case 1: ## A#
		xPos = xPos - 2 * whiteKeyWidth + 5 * blackKeyWidth;
		keyWidth = blackKeyWidth;
		keyHeight = blackKeyLen;
		colour = "black";
		break;
	case 2: ## B
		xPos = xPos + whiteKeyWidth;
		keyWidth = whiteKeyWidth;
		keyHeight = whiteKeyLen;
		colour = "white";
		break;
	case 3: ## C
		xPos = xPos +  2 * whiteKeyWidth;
		keyWidth = whiteKeyWidth;
		keyHeight = whiteKeyLen;
		colour = "white";
		break;
	case 4: ## C#
		xPos = xPos +  2 * whiteKeyWidth + blackKeyWidth * 21/20;
		keyWidth = blackKeyWidth * 21/20;
		keyHeight = blackKeyLen;
		colour = "black";
		break;
	case 5: ## D
		xPos = xPos +  3 * whiteKeyWidth;
		keyWidth = whiteKeyWidth;
		keyHeight = whiteKeyLen;
		colour = "white";
		break;
	case 6: ## D#
		xPos = xPos +  2 * whiteKeyWidth + 3 * blackKeyWidth * 21/20;
		keyWidth = blackKeyWidth * 21/20;
		keyHeight = blackKeyLen;
		colour = "black";
		break;
	case 7: ## E
		xPos = xPos +  4 * whiteKeyWidth;
		keyWidth = whiteKeyWidth;
		keyHeight = whiteKeyLen;
		colour = "white";
		break;
	case 8: ## F
		xPos = xPos +  5 * whiteKeyWidth;
		keyWidth = whiteKeyWidth;
		keyHeight = whiteKeyLen;
		colour = "white";
		break;
	case 9: ## F#
		xPos = xPos +  5 * whiteKeyWidth + blackKeyWidth;
		keyWidth = blackKeyWidth;
		keyHeight = blackKeyLen;
		colour = "black";
		break;
	case 10: ## G
		xPos = xPos +  6 * whiteKeyWidth;
		keyWidth = whiteKeyWidth;
		keyHeight = whiteKeyLen;
		colour = "white";
		break;
	case 11: ## G#
		xPos = xPos +  5 * whiteKeyWidth + 3 * blackKeyWidth;
		keyWidth = blackKeyWidth;
		keyHeight = blackKeyLen;
		colour = "black";
		break;
	}
	if (highlightColour != @) {
		colour = highlightColour;
	}
	return Rectangle(xPos, yOffset, keyWidth, keyHeight, colour);
}

## The keys currently highlighted on the keyboard are the key names in this list.
## E.g. highlightedKeys = ["A0"];
highlightedKeys = [];

## The keys currently highlighted could be shaded in any number of ways.  The
## function keyHighlight determines how the colours are chosen.  The default shading
## scheme uses two colours, one for the natural notes and another for the sharps and
## flats.

##Change these observables to change the highlighting colours.
naturalHighlightColour = "green";
sharpHighlightColour = "lime";

##This is the default highlighting function that determines that all highlighted
##naturals are highlighted in naturalHighlightColour and all highlighted sharps are
##highlighted in sharpHighlightColour.

func twoToneHighlight {
	para highlighted, note;
	if (inList(highlighted, note)) {
		if (substr(note, 2, 2) == sharpSymbol) {
			return sharpHighlightColour;
		} else {
			return naturalHighlightColour;
		}
	} else {
		return @;
	}
}

##The observable keyHighlight determines the actual highlighting function used.
##Reassign this observable to implement more complex highlighting schemes
##(e.g. different colours for keys pressed with the left hand as compared to those
##pressed with the right).

keyHighlight = twoToneHighlight;

##This is a fudge to force things to update properly.
proc changeHighlightColour : naturalHighlightColour, sharpHighlightColour {
	keyHighlight = keyHighlight;
}

##Defines an observable (e.g. keyASharp0) for the visual representation of a
##single piano key, given its piano key number.
proc buildKey {
	para keyNumber;
	auto note, keyDefinition;
	note = noteName(keyNumber);
	note = replace(note, sharpSymbol, "Sharp");

	keyDefinition = "key" // note // " is keyVisual(" // keyNumber;
	keyDefinition = keyDefinition // ", pianoPosition.x, pianoPosition.y, naturalKeyWidth, naturalKeyLength, sharpKeyWidth, sharpKeyLength, keyHighlight(highlightedKeys, \"" // noteName(keyNumber) // "\"));";
	createDefinition(keyDefinition);
}

##Defines observables for the visual representation of a complete piano keyboard.
##keyA0..keyC8 and keyASharp0..keyGSharp7 are Drawable observables representing
##	the individual keys.
##keyboard is the list containing each of the keyXX observables in the order that
##	they are painted onto the screen, all of the natural notes first, then the
##	sharps.
##pianoKeyNumToDrawOrder is a list in piano key order whose elements are the
##	index numbers where the corresponding piano key can be found in the
##	keyboard observable.
##drawOrderToPianoKeyNum is the reverse transformation from screen painting
##	order to piano key number.
proc buildKeys {
	auto keyNumber, octave, keyboardDef, keysListed;
	
	##Define each individual key.
	for (keyNumber = 1; keyNumber <= 88; keyNumber++) {
		buildKey(keyNumber);
	}
	
	##Define the order to draw the keys in and the mappings between the drawing order
	##and piano key number order.
	keyboardDef = "keyboard is [keyA0, keyB0";
	pianoKeyNumToDrawOrder = [1, @, 2, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @, @];
	keysListed = 2;
	drawOrderToPianoKeyNum = [1, 3];
	for (octave = 1; octave <= 7; octave++) {
		keyboardDef = keyboardDef // ", keyC" // octave;
		keyboardDef = keyboardDef // ", keyD" // octave;
		keyboardDef = keyboardDef // ", keyE" // octave;
		keyboardDef = keyboardDef // ", keyF" // octave;
		keyboardDef = keyboardDef // ", keyG" // octave;
		keyboardDef = keyboardDef // ", keyA" // octave;
		keyboardDef = keyboardDef // ", keyB" // octave;

		pianoKeyNumToDrawOrder[(octave - 1) * 12 + 4] = keysListed + 1;
		pianoKeyNumToDrawOrder[(octave - 1) * 12 + 6] = keysListed + 2;
		pianoKeyNumToDrawOrder[(octave - 1) * 12 + 8] = keysListed + 3;
		pianoKeyNumToDrawOrder[(octave - 1) * 12 + 9] = keysListed + 4;
		pianoKeyNumToDrawOrder[(octave - 1) * 12 + 11] = keysListed + 5;
		pianoKeyNumToDrawOrder[(octave - 1) * 12 + 13] = keysListed + 6;
		pianoKeyNumToDrawOrder[(octave - 1) * 12 + 15] = keysListed + 7;
		keysListed = keysListed + 7;
		
		drawOrderToPianoKeyNum = drawOrderToPianoKeyNum // [
			(octave - 1) * 12 + 4,  ## C
			(octave - 1) * 12 + 6,  ## D
			(octave - 1) * 12 + 8,  ## E
			(octave - 1) * 12 + 9,  ## F
			(octave - 1) * 12 + 11, ## G
			(octave - 1) * 12 + 13, ## A
			(octave - 1) * 12 + 15  ## B
		];
		
	}
	
	keyboardDef = keyboardDef // ", keyC8, keyASharp0";
	pianoKeyNumToDrawOrder[88] = keysListed + 1;
	keysListed = keysListed + 1;
	numberOfNaturals = keysListed;
	pianoKeyNumToDrawOrder[2] = keysListed + 1;
	keysListed = keysListed + 1;
	drawOrderToPianoKeyNum = drawOrderToPianoKeyNum // [88, 2];
	
	for (octave = 1; octave <= 7; octave++) {
		keyboardDef = keyboardDef // ", keyCSharp" // octave;
		keyboardDef = keyboardDef // ", keyDSharp" // octave;
		keyboardDef = keyboardDef // ", keyFSharp" // octave;
		keyboardDef = keyboardDef // ", keyGSharp" // octave;
		keyboardDef = keyboardDef // ", keyASharp" // octave;

		pianoKeyNumToDrawOrder[(octave - 1) * 12 + 5] = keysListed + 1;
		pianoKeyNumToDrawOrder[(octave - 1) * 12 + 7] = keysListed + 2;
		pianoKeyNumToDrawOrder[(octave - 1) * 12 + 10] = keysListed + 3;
		pianoKeyNumToDrawOrder[(octave - 1) * 12 + 12] = keysListed + 4;
		pianoKeyNumToDrawOrder[(octave - 1) * 12 + 14] = keysListed + 5;
		keysListed = keysListed + 5;

		##The drawing order of the sharps isn't currently used anywhere in the model.
		drawOrderToPianoKeyNum = drawOrderToPianoKeyNum // [
			(octave - 1) * 12 + 5,  ## C#
			(octave - 1) * 12 + 7,  ## D#
			(octave - 1) * 12 + 10, ## F#
			(octave - 1) * 12 + 12, ## G#
			(octave - 1) * 12 + 14  ## A#
		];
	}
	keyboardDef = keyboardDef // "];";
	createDefinition(keyboardDef);	
}

##Returns the sound wave object corresponding to a given piano key number.
##At the moment the sound is very unrealistic because it only includes a
##single fundamental frequency and no harmonics.
func pianoSound {
	${{
		return new Audio(Notes.getDataURI(arguments[0] - 40));
	}}$;
}

##Defines an observable (e.g. ASharp0Sound) representing the sound of a given
##piano key number.
proc buildSound {
	para keyNumber;
	auto note, soundDefinition;
	note = noteName(keyNumber);
	note = replace(note, sharpSymbol, "Sharp");

	soundDefinition = note // " = pianoSound(" // keyNumber;
	soundDefinition = soundDefinition // ");";
	createDefinition(soundDefinition);
}

##Defines the complete range of piano sounds (or the ones currently supported
##by the model anyway!).
##A0Sound..C8Sound and ASharp0Sound..GSharp7Sound are obserables, each
##representing the sound of a single piano key played for a fixed duration.
##sounds is a list of all of the sounds in piano key order.
proc buildSounds {
	auto keyNumber, octave, soundsDef;

	##Define the sound of each key.
	for (keyNumber = 1; keyNumber <= 88; keyNumber++) {
		buildSound(keyNumber);
	}
	
	##A define a list of sounds in piano key order.
	soundsDef = "pianoKeySounds is [A0Sound, ASharp0Sound, B0";
	for (octave = 1; octave <= 7; octave++) {
		soundsDef = soundsDef // "Sound, C" // octave;
		soundsDef = soundsDef // "Sound, CSharp" // octave;
		soundsDef = soundsDef // "Sound, D" // octave;
		soundsDef = soundsDef // "Sound, DSharp" // octave;
		soundsDef = soundsDef // "Sound, E" // octave;
		soundsDef = soundsDef // "Sound, F" // octave;
		soundsDef = soundsDef // "Sound, FSharp" // octave;
		soundsDef = soundsDef // "Sound, G" // octave;
		soundsDef = soundsDef // "Sound, GSharp" // octave;
		soundsDef = soundsDef // "Sound, A" // octave;
		soundsDef = soundsDef // "Sound, ASharp" // octave;
		soundsDef = soundsDef // "Sound, B" // octave;
	}
	soundsDef = soundsDef // "Sound, C8Sound];";
	createDefinition(soundsDef);
}

buildKeys();
buildSounds();

picture is keyboard;

##The model contains automated agents that temporarily highlight some keys and
##then restore highlightedKeys back to its previous definition when the agents'
##work is done.

##When this observable has the value 1 then a dependency is maintained to ensure
##that the observable highlightedKeysDef always holds the text of the EDEN definition
##last used to change highlightedKeys.  When saveKeyHighlightChanges is 0 then
##highlightedKeys is in the process of being temporarily changed by an automated
##agent and the new temporary definition is not saved.

saveKeyHighlightChanges = 1;

proc backupHighlightedKeys : highlightedKeys {
	if (saveKeyHighlightChanges) {
		highlightedKeysDef = definitionOf("highlightedKeys");
	}
}

##Synchronize highlightedKeysDef with whatever definition of highlightedKeys is already in effect.
backupHighlightedKeys();

##This procedure is such an automated agent that temporarily modifies the highlighting.
proc playNoteWhenClicked : mousePressed {
	auto middleKeyDrawOrder, middleKeyNum, leftKeyRect, rightKeyRect, pianoKeyHit, savingKeyHighlightChanges;
	savingKeyHighlightChanges = saveKeyHighlightChanges;
	saveKeyHighlightChanges = 0;
	if (mousePressed) {
		if (mouseX >= pianoPosition.x && mouseX < pianoPosition.x + numberOfNaturals * naturalKeyWidth &&
		 mouseY >= pianoPosition.y && mouseY < pianoPosition.y + naturalKeyLength) {			
			
			##Find out which piano key has been clicked.  We first locate the white key
			##whose zone the mouse pointer is within.  The user might actually have
			##clicked on one of the black keys that are located on either side, so we
			##check the bounds of the rectangle representing the key that is one piano
			##key number lower than our initial guess (irrespective of whether the key
			##to the left is black or white, though the check is unnecessary if it is in
			##fact white) and similarly for the rectangle representing the key that
			##is one piano key number higher than the initial guess.  If we haven't
			##clicked on a sharp key that's slightly to the left or slightly to the right
			##of our initial guess then our initially guessed white key was the key that
			##was actually clicked.
			
			middleKeyDrawOrder = int((mouseX - pianoPosition.x) / naturalKeyWidth) + 1;
			middleKeyNum = drawOrderToPianoKeyNum[middleKeyDrawOrder];
			pianoKeyHit = @;
			
			if (middleKeyNum > 1) {
				leftKeyRect = keyboard[pianoKeyNumToDrawOrder[middleKeyNum - 1]];
				if (mouseX >= leftKeyRect.x1 && mouseX < leftKeyRect.x1 + leftKeyRect.x2 && mouseY >= leftKeyRect.y1 && mouseY < leftKeyRect.y1 + leftKeyRect.y2) {
					pianoKeyHit = middleKeyNum - 1;
				}
			}
			
			if (middleKeyDrawOrder < numberOfNaturals) {
				rightKeyRect = keyboard[pianoKeyNumToDrawOrder[middleKeyNum + 1]];
				if (mouseX >= rightKeyRect.x1 && mouseX < rightKeyRect.x1 + rightKeyRect.x2 && mouseY >= rightKeyRect.y1 && mouseY < rightKeyRect.y1 + rightKeyRect.y2) {
					pianoKeyHit = middleKeyNum + 1;
				}				
			}
			
			if (pianoKeyHit == @) {
				pianoKeyHit = middleKeyNum;
			}
			
			highlightedKeys = [noteName(pianoKeyHit)];
			pianoKeySounds[pianoKeyHit].play();
		}
	} else {
		execute(highlightedKeysDef);
	}
	saveKeyHighlightChanges = savingKeyHighlightChanges;
}
